#!/bin/zsh
autoload colors
colors

echo "$fg_bold[white]Numbered colors, may require zsh-256color plugin.$fg_no_bold[default]"
echo "Use with print -P %K{} (background) or %F{} (foreground), %f and %k for reset."
echo
for row in {1..25}; do
    for column in {0..10}; do
        color=$((25*column + row ))
        if [[ "$color" -gt 255 ]]; then
            break
        fi
        colorStr=$(printf '%-3s' "$color")
        print -rPn "%K{$color}   ${colorStr}   %k "
    done
    print
done

echo "$fg_bold[white]You can also use normal RGB if the shell supports it.$fg_no_bold[default]"
echo "Again use with print -P %K{} (background) or %F{} (foreground), %f and %k for reset."
print -P "For example: %F{#cc8020}%%F{#cc8020}%f"
echo

echo "$fg_bold[white]Basic named colors and their highlights:$fg_no_bold[default]"
for color in default black red green yellow ; do
    echo -n "$bg[$color]   \$bg[$color]    $bg[default] "
done
echo
for color in default black red green yellow ; do
    echo -n "$fg[$color]   \$fg[$color]    $fg[default] "
done
echo
for color in default black red green yellow ; do
    echo -n "$fg_bold[$color] \$fg_bold[$color] $fg_no_bold[default] "
done
echo
echo
for color in  blue magenta cyan white; do
    echo -n "$bg[$color]   \$bg[$color]    $bg[default] "
done
echo
for color in  blue magenta cyan white; do
    echo -n "$fg[$color]   \$fg[$color]    $fg[default] "
done
echo
for color in  blue magenta cyan white; do
    echo -n "$fg_bold[$color] \$fg_bold[$color] $fg_no_bold[default] "
done
echo
echo

echo "$fg_bold[white]Use colors in zsh scripts:$fg_no_bold[default]"
print -rP "#!/bin/zsh"
print -rP "autoload colors"
print -rP "colors"
print -rP "print -P \"%%K{\$bgcolor}%%F{\$fgcolor} colorized %%f%%k\", for example: %F{214}%%F{214}%K{125}%%K{125}%%k%k%%f%f"
print -rP "echo \"\$fg_bold[\$c1]\$bg[\$c2] colorized \$bg[default]\$fg_no_bold[default]\", for example: $fg_bold[cyan]\$fg_bold[cyan]$bg[yellow]\$bg[yellow] colorized \$bg[default]$bg[default]\$fg_no_bold[default]$fg_no_bold[default]"
echo
echo "Echo can print out the %K and %F only in shell prompt. Outside of that, you are limited to control sequences, even if streamlined via \$fg_bold variables and such."
echo "And then you cannot even use normal RGB syntax, but something like \e[38;2;255;255;255m \\ e[38;2;255;255;255m \e[0m"
echo

echo "$fg_bold[white]Currently used color theme:$fg_no_bold[default]"
echo "Those colors are part of this zsh environment and dynamically configured in rc/colors.rc."
echo 
# Try to source rc/colors.rc to load semantic COLOR_* variables
if [[ -z "${COLOR_PATH_BG+x}" ]]; then
  # Resolve repo root when script is invoked via PATH
  script_path="${(%):-%N}"
  script_dir="${script_path:A:h}"
  repo_dir="${script_dir:h}"
  if [[ -f "${repo_dir}/rc/colors.rc" ]]; then
    source "${repo_dir}/rc/colors.rc"
  elif [[ -f "rc/colors.rc" ]]; then
    source "rc/colors.rc"
  else
    echo "rc/colors.rc not found; skipping theme colors section."
  fi
fi

# Display detection context
print -r -- "PROMPT_COLOR_MODE=${PROMPT_COLOR_MODE:-<unset>}  SOLARIZED_THEME=${SOLARIZED_THEME:-<unset>}"

# Helper to show one semantic color both as fg and bg
show_one() {
  local label="$1" vname="$2" val
  val="${(P)vname}"
  if [[ -z "$val" ]]; then
    print -r -- "$label: <unset>"
  else
    print -rP "$label: %F{$val}fg%f  %K{$val}    %k  ($val)"
  fi
}

# List of semantic colors to present (label varname)
typeset -a _color_vars
_color_vars=(
  "TIME_BG COLOR_TIME_BG"
  "TIME_FG COLOR_TIME_FG"
  "TIME_SEP_FG COLOR_TIME_SEP_FG"
  "PATH_BG COLOR_PATH_BG"
  "PATH_FG COLOR_PATH_FG"
  "GIT_CLEAN_BG COLOR_GIT_CLEAN_BG"
  "GIT_CLEAN_FG COLOR_GIT_CLEAN_FG"
  "GIT_DIRTY_BG COLOR_GIT_DIRTY_BG"
  "GIT_DIRTY_FG COLOR_GIT_DIRTY_FG"
  "STATUS_BG COLOR_STATUS_BG"
  "STATUS_FG COLOR_STATUS_FG"
  "STATUS_ERR_FG COLOR_STATUS_ERR_FG"
  "STATUS_JOBS_FG COLOR_STATUS_JOBS_FG"
  "STATUS_ROOT_FG COLOR_STATUS_ROOT_FG"
  "VENV_BG COLOR_VENV_BG"
  "VENV_FG COLOR_VENV_FG"
  "AWS_PROD_BG COLOR_AWS_PROD_BG"
  "AWS_PROD_FG COLOR_AWS_PROD_FG"
  "AWS_NONPROD_BG COLOR_AWS_NONPROD_BG"
  "AWS_NONPROD_FG COLOR_AWS_NONPROD_FG"
  "CONTEXT_BG COLOR_CONTEXT_BG"
  "CONTEXT_FG COLOR_CONTEXT_FG"
  "CONTEXT_ROOT_BG COLOR_CONTEXT_ROOT_BG"
  "CONTEXT_ROOT_FG COLOR_CONTEXT_ROOT_FG"
  "GREY_FG COLOR_GREY_FG"
  "DEFAULT_SEP_FG AGNOSTER_DEFAULT_SEP_FG"
)

echo
echo "$fg_bold[white]Theme semantic colors (from rc/colors.rc):$fg_no_bold[default]"
local pair
for pair in "${_color_vars[@]}"; do
  show_one ${=pair}
done

unset pair _color_vars script_path script_dir repo_dir


